plugins {
    id 'org.springframework.boot' version '2.5.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'org.owpk'

def producerModule = project(":message-producer")
def consumerModule = project(":message-consumer")
def globalModule = project(":global")

repositories {
    mavenCentral()
}

configure ([producerModule, consumerModule, globalModule]) {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    sourceCompatibility = '11'
    targetCompatibility = '11'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-amqp'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.amqp:spring-rabbit-test'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok:1.18.20'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            outputs.upToDateWhen { false }
            showStandardStreams = true
        }
    }
}

configure ([producerModule, consumerModule]) {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation globalModule
    }
}

configure producerModule, {
    apply plugin: 'war'

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly 'org.postgresql:postgresql'
        runtimeOnly group: 'com.h2database', name: 'h2', version: '1.4.200'
    }
}

configure consumerModule, {
    dependencies {
        implementation group: 'javax.mail', name: 'mail', version: '1.4'
        implementation group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version: '1.2.0.RELEASE'
    }
}

configure globalModule, {
    tasks.getByName "bootJar", {
        enabled = false
    }

    tasks.getByName "jar", {
        enabled = true
    }
}

tasks.getByName "bootJar", {
    enabled = false
}
